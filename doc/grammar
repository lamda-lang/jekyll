Module = ( <_> | DefinitionAll | Use | Import )*
DefinitionAll = (Definition | Function)
Definition = Identifier <_*> <'='> <_*> Expression [<_+> Where]
Signature = Identifier <'('> <_*> Args <_*> <')'>
Function = Signature <_*> <'='> <_*> Expression [<_+> Where]
Keyword = 'nil' | 'true' | 'false' | 'when' | 'case' | 'do' | 'if' | 'end' | 'use' | 'import' | 'only' | 'hide' | 'type'
Component = !Keyword #'[A-Za-z_]\w*'
Expressions = (Expression <_+>)* Expression
Expression = Token | Range | Integer | String | Nil | Boolean | Result | Lamda | List | Map | Set | When | Case | Identifier | Float | Do | ListComprehension | SetComprehension | MapComprehension | Type | Protocol
KeyValues = (KeyValue <_+>)* KeyValue
KeyValue = Expression <_*> <':'> <_*> Expression
Result = ( Identifier | Result | Lamda ) <'('>  <_*> [Expressions <_*>] <')'>
Lamda = <'('> <_*> Args <':'> <_*> Expression <_*> <')'>
Where = <'where'> <_+> (DefinitionAll <_*>)* <'end'>
When = <'when'> <_+> (KeyValue <_*>)* <'end'>
Case = <'case'> <_+> Identifier <_+> (Branch <_+>)* <'end'>
Identifier = Component (<'.'> Component)*
Guard = <'if'> <_+> Expression
Do = <'do'> <_+> (DefinitionAll <_*>)*  <'end'>
Branch = Expression [<_+> Guard] <_*> <':'> <_*> Expression
Namespace = (Identifier <'.'>)* Identifier
Use = <'use'> <_+> Namespace [<_+> ('only'|'hide') (<_+> Identifier)+ ]
Import = <'import'> <_+> Namespace [<_+> 'as' <_+> Identifier ]
ListComprehension = <'['> <_*> Expression <_+> <'for'> <_+> KeyValue [<_+> Guard] <_*> <']'>
MapComprehension = <'{'> <_*> KeyValue <_+> <'for'> <_+> KeyValue [<_+> Guard] <_*> <'}'>
SetComprehension = <'('> <_*> Expression <_+> <'for'> <_+> KeyValue [<_+> Guard] <_*> <')'>
Args = (Identifier <_*>)*
Float = #'-?\d+\.\d+'
Range = ( Expression | Epsilon ) '..' ( Expression | Epsilon )
Integer = #'-?\d+'
String = <'\"'> #'[^"]*' <'\"'>
Token = <'#'> #'\w+'
Nil = <'nil'>
Boolean = 'true' | 'false'
List = <'['> [<_*> Expressions] <_*> <']'>
Map =  <'{'> [<_*> KeyValues] <_*> <'}'>
Set =  <'('> [<_*> Expressions] <_*> <')'>
Type = <'type'> <_*> (Identifier <_*>)+ <'end'>
Protocol = <'protocol'> <_*> (Signature <_*>)+ <'end'>
_ = #'\s'
